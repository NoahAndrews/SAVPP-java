buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'org.standardout:gradle-versioneye-plugin:1.4.0'
    }
}

apply plugin: 'org.standardout.versioneye'

versioneye {
    includeSubProjects = true
    dependencies = transitive
    includePlugins = false
    exclude 'testCompile', 'testRuntime', 'testCompileClasspath', 'testCompileOnly'
}

versionEyeUpdate {
    enabled = (System.getenv('CI') != 'true')
}

allprojects {
    group 'me.noahandrews.savpp'
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'maven'
    apply plugin: 'signing'

    repositories {
        jcenter()
    }

    dependencies {
        compile 'org.apache.logging.log4j:log4j-api:2.6.1'
        testCompile 'org.apache.logging.log4j:log4j-core:2.6.1'

        testCompile 'junit:junit:4.12'
        testCompile 'org.mockito:mockito-core:1.+'
    }

    assemble.finalizedBy(versionEyeUpdate)

    tasks.withType(JavaCompile) {
        options.compilerArgs << "-Xlint:deprecation"
    }

    sourceCompatibility = 1.8

    test {
        testLogging {
            events "passed", "skipped", "failed", "standardOut", "standardError"
        }
    }


    task sourcesJar(type: Jar) {
        classifier = 'source'
        from sourceSets.main.allSource
    }

    task javadocJar(type: Jar) {
        classifier = 'javadoc'
        from javadoc
    }

    signing {
        sign configurations.archives
    }

    signArchives.enabled = (System.getenv('CI') != 'true')

    artifacts {
        archives javadocJar, sourcesJar
    }

    uploadArchives {
        repositories {
            mavenDeployer {
                beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment)}

                repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                    authentication(userName: OSSRH_USERNAME, password: OSSRH_PASSWORD)
                }

                snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                    authentication(userName: OSSRH_USERNAME, password: OSSRH_PASSWORD)
                }

                pom.project {
                    name 'SAVPP Server Library'
                    packaging 'jar'
                    description 'A library that allows an application to act as a Synchronized Audio/Video Playback Protocol (SAVPP) server.'
                    url 'http://savpp.noahandrews.me'

                    scm {
                        connection 'scm:git:https://github.com/NoahAndrews/SAVPP-java.git'
                        developerConnection 'scm:git:https://github.com/NoahAndrews/SAVPP-java.git'
                        url 'https://github.com/NoahAndrews/SAVPP-java'
                    }

                    licenses {
                        license {
                            name 'MIT License'
                            url 'http://opensource.org/licenses/mit-license.php'
                        }
                    }

                    developers {
                        developer {
                            id 'NoahAndrews'
                            name 'Noah Andrews'
                            email 'NoahAndrews@users.noreply.github.com'
                        }
                    }
                }
            }
        }
    }
}
